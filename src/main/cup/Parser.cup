package ru.omsu.translator.cup;

import java.util.HashMap;
import java.util.Map;
import java_cup.runtime.*;
import ru.omsu.translator.CustomSymbol;
import ru.omsu.translator.Token;

import java.io.File;
import java.io.FileWriter;
import java.io.IOException;

import java_cup.parser;

init with{:
    System.out.println("старт парсинга");
:}

parser code {:

    protected Map<String, Integer> varIndexes = new HashMap<>();
    protected int varCounter = 0;
    protected StringBuilder jasmin = new StringBuilder();

    private int getVarIndex(String varName) {
        return varIndexes.computeIfAbsent(varName, k -> varCounter++);
    }

    private void emit(String code) {
        System.out.println("Добавлена строчка Jasmin (CUP)" + code);
        jasmin.append("    ").append(code).append("\n");
    }

    String folderPath = "src/main/java/ru/omsu/translator/jasmin_code";
    String fileName = "Main.j";

    File directory = new File(folderPath);
    File file = new File(directory, fileName);

    public void getJasminCode() {
        try {
            if (!directory.exists()) {
                directory.mkdirs();
            }
            if (file.createNewFile()) {
                System.out.println("Файл создан: " + file.getAbsolutePath());
            } else {
                System.out.println("Файл уже существует: " + file.getAbsolutePath());
            }
                FileWriter writer = new FileWriter(file);
                writer.write(
                        ".class public Main\n"+
                        ".super java/lang/Object\n"+
                        ".method public static main([Ljava/lang/String;)V\n"+
                        ".limit stack 100\n"+
                        ".limit locals 100\n"+
                jasmin.toString());
                writer.close();
                System.out.println("Запись завершена.");
            } catch (IOException e) {
                e.printStackTrace();
            }
    }

:};


terminal CustomSymbol KEYWORD, STRING, CHAR,
                      LPAREN, RPAREN, LBRACKET, RBRACKET,
                      BEGIN, END, WRITE, READ, ASSIGN,
                      SEMICOLON;

terminal String OPERATOR, IDENTIFIER;
terminal Integer NUMBER;

non terminal program, optional_statements, statement_list, statement;
non terminal String expr, term, factor;

precedence left OPERATOR;
precedence left SEMICOLON;

start with program;

program ::= BEGIN optional_statements END
    {:
        emit("    return");
        emit(".end method");

        getJasminCode();

    :}
    | error
    {: System.err.println("Syntax error in program"); :};

optional_statements ::= statement_list
                      | /* empty */;

statement_list ::= statement SEMICOLON statement_list
                 | statement SEMICOLON
                 ;

statement ::= IDENTIFIER:id ASSIGN expr:e
    {:
        String name = (String)id;
        int index = getVarIndex(name);
        emit(e); // e — это String, сгенерированный в expr
        emit("istore " + index);
    :}

           | WRITE LPAREN IDENTIFIER:id RPAREN
    {:
        String name = (String)id;
        int index = getVarIndex(name);
        emit("getstatic java/lang/System/out Ljava/io/PrintStream;");
        emit("iload " + index);
        emit("invokevirtual java/io/PrintStream/println(I)V");
    :}
           | READ LPAREN IDENTIFIER:id RPAREN
    {:
        String name = (String)id;
        int index = getVarIndex(name);
        emit("new java/util/Scanner");
        emit("dup");
        emit("getstatic java/lang/System/in Ljava/io/InputStream;");
        emit("invokespecial java/util/Scanner/<init>(Ljava/io/InputStream;)V");
        emit("invokevirtual java/util/Scanner/nextInt()I");
        emit("istore " + index);
    :};
expr ::= expr:a OPERATOR:op term:b
    {:
        String operator = (String)op;
        StringBuilder sb = new StringBuilder();
        sb.append(a).append(b); // Подвыражения тоже возвращают строки
        switch (operator) {
            case "+": sb.append("    iadd\n"); break;
            case "-": sb.append("    isub\n"); break;
        }
        RESULT = sb.toString();
    :}
      | term:t
    {:
        RESULT = t;
    :};
term ::= term:a OPERATOR:op factor:b
    {:
        String operator = (String)op;
        StringBuilder sb = new StringBuilder();
        sb.append(a).append(b);
        switch (operator) {
            case "*": sb.append("    imul\n"); break;
            case "/": sb.append("    idiv\n"); break;
        }
        RESULT = sb.toString();
    :}
     | factor:f
    {:
        RESULT = f;
    :};
factor ::= NUMBER:n
    {:
        int val = (Integer) n;
        RESULT = "    ldc " + val + "\n";
    :}
         | IDENTIFIER:id
    {:
        String name = (String)id;
        int index = getVarIndex(name);
        RESULT = "    iload " + index + "\n";
    :}
         | LPAREN expr:e RPAREN
    {:
        RESULT = e;
    :};